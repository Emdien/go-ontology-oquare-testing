# # Missing Superclass
#
# **Problem:** A class does not have a superclass. This is not relevant for top-level classes, but may reveal orphaned children. Excludes deprecated entities.
#
# **Solution:** Make sure there are no orphaned children - if so, assert a parent.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX GO: <http://purl.obolibrary.org/obo/GO_>

SELECT DISTINCT ?entity 
WHERE {
  VALUES ?roots { GO:0003674 GO:0005575 GO:0008150 }
  ?entity a owl:Class .
  
  FILTER (!isBlank(?entity)) .
  FILTER NOT EXISTS { ?entity owl:deprecated true } .
  FILTER strstarts( str(?entity), "http://purl.obolibrary.org/obo/GO_" ) .
  FILTER NOT EXISTS { ?entity rdfs:subClassOf ?super } .
  FILTER (?entity != GO:0003674) .
  FILTER (?entity != GO:0005575) .
  FILTER (?entity != GO:0008150) .
}
ORDER BY ?entity
